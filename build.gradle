/*******************************************************************************
 * Copyright (c) 2020 ArSysOp
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 *
 * Contributors:
 *     ArSysOp - initial API and implementation
 *******************************************************************************/

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
}

group = 'ru.arsysop.liho'
version = currentVersion

repositories {
    jcenter()
    maven {
        url = "https://dl.bintray.com/arsysop/lang"
    }
    maven {
        url = "https://dl.bintray.com/arsysop/liho"
    }
}

dependencies {
    implementation "ru.arsysop.liho:liho:0.1"
}

gradlePlugin {
    plugins {
        liho {
            id = 'ru.arsysop.liho.liho-gradle-plugin'
            displayName = 'LiHo: License header checker'
            description = 'Check if you source files contain proper license headers'
            implementationClass = 'ru.arsysop.liho.gradle.LihoPlugin'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadocJar {
    dependsOn += "groovydoc"
    from(groovydoc.destinationDir)
}

tasks.withType(Jar) {
    from('README.md', 'LICENSE')
    extendManifest(manifest)
}

void extendManifest(Manifest mf) {
    mf.attributes([
            "Group"   : project.group,
            "Artifact": project.name,
            "Version" : project.version
    ])
}

publishing {
    repositories {
        maven {
            url = uri("$buildDir/local-repo")
        }
    }
    publications {
        release(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = "https://github.com/ArSysOp/liho-gradle-plugin"
                licenses {
                    license {
                        name = "Apache 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0"
                    }
                }
                developers {
                    developer {
                        id = "eparovyhsnaya"
                        name = "Elena Parovyshnaia"
                        email = "elena.parovyshnaya@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/arsysop/liho-gradle-plugin.git"
                    developerConnection = "scm:git:ssh://github.com/arsysop/liho-gradle-plugin.git"
                    url = "https://github.com/arsysop/liho-gradle-plugin"
                }
            }
        }
    }
}
